#include "interfaces/UserModule_Interface.hps"
#include "modules/ModuleInterfaces.hps"

#include "helpers/helper_imgui.hps"
#include "helpers/helper_player.hps"

class cScrStaminaHandler : iScrUserModule, iScrUserModule_Interface
{
	//////////////////////////////////////////////////////////////////////////////////////////
    // ==============
    // PROPERTIES
    // ==============
    //{///////////////////////////////////////////////////////////////////////////////////////
	
	// Float
	float mfStaminaTotal;
	float mfStaminaMax;
	float mfStaminaRegainMul;
	float mfStaminaUseMul;
	float mfStaminaRegainMulCounter;
	
	// Bool
	bool mbStaminaRegain;
	bool mbStaminaDrain;
	bool mbRunningAffectStamina;
	bool mbStaminaAffectRunSpeed;
	
	bool mbStaminaSystemActive;
	
	// Debug data
	cVector2f mvDebugFontSize;
	
    //////////////////////////////////////////////////////////////////////////////////////////
    // ==============
    // MAIN CALLBACKS
    // ==============
    //{///////////////////////////////////////////////////////////////////////////////////////
    
    //-------------------------------------------------------
    
    void Init() 
	{
		mfStaminaTotal = 100.00f;
		mfStaminaMax = 100.0f;
		mfStaminaRegainMul = 0.25f;
		mfStaminaUseMul = 1.0f;
		mfStaminaRegainMulCounter = 0.0f;
		
		mbStaminaRegain = true;
		mbStaminaDrain = true;
		mbRunningAffectStamina = true;
		mbStaminaAffectRunSpeed = true;
		
		mbStaminaSystemActive = false;
		
		mvDebugFontSize = cVector2f(16.0f, 16.0f);
	}
	
	void Update(float afTimeStep) 
	{
		StartStaminaSystem();
	}
	
	void Reset() 
	{
		mfStaminaTotal = 100.00f;
		mfStaminaMax = 100.0f;
		mfStaminaRegainMul = 0.25f;
		mfStaminaUseMul = 1.0f;
		mfStaminaRegainMulCounter = 0.0f;
		
		mbStaminaRegain = true;
		mbStaminaDrain = true;
		mbRunningAffectStamina = true;
		mbStaminaAffectRunSpeed = true;
		
		mbStaminaSystemActive = false;
	}
	
    void PostUpdate(float afTimeStep) {}
    void VariableUpdate(float afDeltaTime) {}
    void OnDraw(float afFrameTime) {}
    void OnPostRender(float afFrameTime) {}
    void LoadUserConfig() {}
    void SaveUserConfig() {}
    void OnMapEnter(cLuxMap @apMap) {}
    void OnMapLeave(cLuxMap @apMap) {}
    void CreateWorldEntities(cLuxMap @apMap) {}
    void DestroyWorldEntities(cLuxMap @apMap) {}
    void PreloadData(cLuxMap @apMap) {}
    void OnEnterContainer(const tString&in asOldContainer) {}
    void OnLeaveContainer(const tString&in asNewContainer) {}
    void OnExitPressed() {}
    void OnAction(int alAction,  bool abPressed) {}
    void OnAnalogInput(int alAnalogId,  const cVector3f &in avAmount) {}
    void AppGotInputFocus() {}
    void AppLostInputFocus() {}
    
    //-------------------------------------------------------
    
    //} END MAIN CALLBACKS
    
    //////////////////////////////////////////////////////////////////////////////////////////
    // ================
    // GLOBAL FUNCTIONS
    // ================
    //{///////////////////////////////////////////////////////////////////////////////////////
    
    //-------------------------------------------------------
	
	/**
	 * Runs the handlers for managing the players stamina, runs at every frame.
	 * This must be called inside the Update function within the cScrMap class in the [map].hps file!
	 * //
	 * The player has at default limited stamina to run. The stamina's value is at a range of 0 - 100.
	 * 
	 * @param float afTimeStep, the time between each step (1s/60fps = 0.01667s/fps)
	**/
	void StartStaminaSystem()
	{
		if (!mbStaminaSystemActive)
		{ 
			//Error("The stamina system is not active, use Player_StaminaSystemCreateInstance().SetStaminaSystemActive(true)");
			return; 
		}
	
		StaminaRunningHandler();
		PlayerRunningSpeedHandler();
		RegainStaminaHandler();
		return;
	}
	
    //-------------------------------------------------------
	
	/**
	 * A handler for decrementing the players stamina while running, runs at every frame.
	**/
	void StaminaRunningHandler()
	{
		if (mbRunningAffectStamina)
		{
			if (mfStaminaRegainMulCounter > mfStaminaRegainMul) { mfStaminaRegainMulCounter = mfStaminaRegainMul; }
			//if (mfStaminaTotal < 0) { mfStaminaTotal = 0.0f; }
			//Remove && mfStaminaTotal >= 0.1f
			
			// Decrement the total stamina if player is running
			if (Player_GetRunning() && mbStaminaDrain && mfStaminaTotal >= 0.1f)
			{
				mfStaminaRegainMulCounter = 0.0f;
				mfStaminaTotal -= 0.1f*mfStaminaUseMul;
				return;
			}
		}
	}
	
    //-------------------------------------------------------
	
	/**
	 * A handler for incrementing the players stamina while idle/walking, runs at every frame.
	 * Lowers running speed when mfStaminaTotal < mfStaminaMax/2.0f.
	 * Resets running speed when mfStaminaTotal > mfStaminaMax/2.0f.
	**/
	void PlayerRunningSpeedHandler()
	{
		if (mbStaminaAffectRunSpeed)
		{
			if (mfStaminaTotal < mfStaminaMax/2.0f)
			{
				Player_SetRunSpeedMul(mfStaminaTotal/100.0f);
				return;
			}
			else if (mfStaminaTotal > mfStaminaMax/2.0f)
			{
				Player_SetRunSpeedMul(1.0f);
				return;
			}
			
		}
	}
	
    //-------------------------------------------------------
	
	/**
	 * A handler for incrementing the players stamina while idle/walking, runs at every frame.
	**/
	void RegainStaminaHandler()
	{
		if (!Player_GetRunning() && mbStaminaRegain && mfStaminaTotal < mfStaminaMax && mfStaminaRegainMulCounter <= mfStaminaRegainMul)
		{	
			mfStaminaRegainMulCounter += 0.005f+mfStaminaRegainMul/10.0f;
			mfStaminaTotal += 0.05f*mfStaminaRegainMulCounter;
			return;
		}
	}
	
    //-------------------------------------------------------
	
	void _Global_SetStaminaSystemActive()
	{
		bool mbSetStaminaActive = cScript_GetGlobalArgBool(0);
		
		if (mbSetStaminaActive)
		{
			mbStaminaSystemActive = mbSetStaminaActive;
			return;
		}
		else
		{
			Reset();
			return;
		}
	}
	
    //-------------------------------------------------------
	
	void _Global_GetStaminaSystemActive()
	{
		cScript_SetGlobalReturnBool(mbStaminaSystemActive);
	}
	
    //-------------------------------------------------------
	
	void _Global_SetStaminaDrain()
	{
		bool abActive = cScript_GetGlobalArgBool(0);
		mbStaminaDrain = abActive;
		
		if (!abActive)
		{
			mfStaminaTotal = mfStaminaMax;
			return;
		}
		return;
	}
	
	//-------------------------------------------------------
	
	void _Global_GetStaminaDrain()
	{
		cScript_SetGlobalReturnBool(mbStaminaDrain);
	}
	
	//-------------------------------------------------------
	
	void _Global_SetStaminaRegain()
	{
		mbStaminaRegain = cScript_GetGlobalArgBool(0);
	}
	
	//-------------------------------------------------------
	
	void _Global_GetStaminaRegain()
	{
		cScript_SetGlobalReturnBool(mbStaminaRegain);
	}
	
	//-------------------------------------------------------
	
	void _Global_SetRunningAffectStamina()
	{
		bool abActive = cScript_GetGlobalArgBool(0);
		mbRunningAffectStamina = abActive;
		
		if (!abActive)
		{
			mfStaminaTotal = mfStaminaMax;
			return;
		}
		return;
	}
	
	//-------------------------------------------------------
	
	void _Global_GetRunningAffectStamina()
	{
		cScript_SetGlobalReturnBool(mbRunningAffectStamina);
	}
	
	//-------------------------------------------------------
	
	void _Global_SetStaminaAffectRunSpeed()
	{
		mbStaminaAffectRunSpeed = cScript_GetGlobalArgBool(0);
	}
	
	//-------------------------------------------------------
	
	void _Global_GetStaminaAffectRunSpeed()
	{
		cScript_SetGlobalReturnBool(mbStaminaAffectRunSpeed);
	}
	
	//-------------------------------------------------------
	
	void _Global_SetStaminaRegainMul()
	{
		float afX = cScript_GetGlobalArgFloat(0);
		
		if (afX > 10.0f || afX <= 0)
		{
			Error("The value range for the stamina multiplier is (> 0 - 10.0), " + cString_ToString(afX, 1.0f) + " is out of range.");
			return;
		}
		else
		{
			mfStaminaRegainMul = afX;
			return;
		}
	}
	
	//-------------------------------------------------------
	
	void _Global_GetStaminaRegainMul()
	{
		cScript_SetGlobalReturnFloat(mfStaminaRegainMul);
	}
	
	//-------------------------------------------------------
	
	void _Global_SetStaminaUsageMul()
	{
		float afX = cScript_GetGlobalArgFloat(0);
		
		if (afX > 10.0f || afX <= 0)
		{
			Error("The value range for the stamina multiplier is (> 0 - 10.0), " + cString_ToString(afX, 1.0f) + " is out of range.");
			return;
		}
		else
		{
			mfStaminaUseMul = afX;
		}
	}
	
	//-------------------------------------------------------
	
	void _Global_GetStaminaUsageMul()
	{
		cScript_SetGlobalReturnFloat(mfStaminaUseMul);
	}
	
	//-------------------------------------------------------

	void _Global_SetStamina()
	{
		float afX = cScript_GetGlobalArgFloat(0);
		
		if (afX > mfStaminaMax || afX < 0)
		{
			Error("The value range for the stamina is (0.0 - " + cString_ToString(mfStaminaMax, 1.0f) + "), " + cString_ToString(afX, 1.0f) + " is out of range.");
			return;
		}
		else
		{
			mfStaminaTotal = afX;
			return;
		}
	}
	
	//-------------------------------------------------------
	
	void _Global_GetStamina()
	{
		cScript_SetGlobalReturnFloat(mfStaminaTotal);
	}
	
	//-------------------------------------------------------
	
	void _Global_SetMaxStamina()
	{
		float afX = cScript_GetGlobalArgFloat(0);
		
		if (afX > 1000.0f || afX < 0)
		{
			Error("The value range for the maximum stamina is (0.0 - 1000.0), " + cString_ToString(afX, 1.0f) + " is out of range.");
			return;
		}
		else
		{
			mfStaminaMax = afX;
			mfStaminaTotal = afX;
			return;
		}
	}
	
    //-------------------------------------------------------
		
	void _Global_GetMaxStamina()
	{
		cScript_SetGlobalReturnFloat(mfStaminaMax);
	}
	
	//-------------------------------------------------------
    
    //} END GLOBAL FUNCTIONS
    
    //////////////////////////////////////////////////////////////////////////////////////////
    // =============
    // GUI CALLBACKS
    // =============
    //{///////////////////////////////////////////////////////////////////////////////////////
    
    //-------------------------------------------------------
    
    void OnGui(float afTimeStep) 
	{
		DrawDebugInfo();
	}
    
    //-------------------------------------------------------
	
	/**
	 * Draws debug info of the stamina system instance, making the debugging process easier.
	 * Only draws the debug info if cLux_ScriptDebugOn() returns true. This is used for debugging!
	**/
	void DrawDebugInfo()
	{
		if (cLux_ScriptDebugOn())
		{
			array<tString> mvStaminaSystemData = GetStaminaSystemData();
			cImGuiLabelData mLabelData = GetLabelData();
			
			ImGui_DoLabelExt("Max Stamina: " + mvStaminaSystemData[1], mLabelData, ImGui_NrmPos(0.615f, 0.4625f, 10.0f), ImGui_NrmSize(1.0f,1.0f));
			ImGui_DoLabelExt("Stamina: " + mvStaminaSystemData[0], mLabelData, ImGui_NrmPos(0.705f, 0.4625f, 10.0f), ImGui_NrmSize(1.0f,1.0f));
			ImGui_DoLabelExt("StaminaSystemEnabled: " + mvStaminaSystemData[2], mLabelData, ImGui_NrmPos(0.775f, 0.4625f, 10.0f), ImGui_NrmSize(1.0f,1.0f));
			ImGui_DoLabelExt("RunAffectStamina: " + mvStaminaSystemData[3], mLabelData, ImGui_NrmPos(0.615f, 0.4835f, 10.0f), ImGui_NrmSize(1.0f,1.0f));
			ImGui_DoLabelExt("StaminaAffectRunSpeed: " + mvStaminaSystemData[4], mLabelData, ImGui_NrmPos(0.715f, 0.4835f, 10.0f), ImGui_NrmSize(1.0f,1.0f));
			ImGui_DoLabelExt("StaminaDrain: " + mvStaminaSystemData[5], mLabelData, ImGui_NrmPos(0.8355f, 0.4835f, 10.0f), ImGui_NrmSize(1.0f,1.0f));
			ImGui_DoLabelExt("StaminaRegain: " + mvStaminaSystemData[6], mLabelData, ImGui_NrmPos(0.916f, 0.4835f, 10.0f), ImGui_NrmSize(1.0f,1.0f));
			return;
		}
	}
	
	//-------------------------------------------------------
	
	/**
	 * Gets the settings of the stamina system. This is used for debugging!
	 * 
	 * @returns array<tString>, an array of the stamina system settings.
	**/
	array<tString> GetStaminaSystemData()
	{
		tString msGuiTextStaminaTotal = cString_ToString(mfStaminaTotal, 2.0f);
		tString msGuiTextStaminaMax = cString_ToString(mfStaminaMax, 2.0f);
		
		bool mbGuiTextStaminaActive = mbStaminaSystemActive;
		bool mbGuiTextRunningAffectStamina = mbRunningAffectStamina;
		bool mbGuiTextStaminaAffectRunSpeed = mbStaminaAffectRunSpeed;
		bool mbGuiTextStaminaDrain = mbStaminaDrain;
		bool mbGuiTextStaminaRegain = mbStaminaRegain;
		
		array<tString> mvStaminaSystemData = {
			msGuiTextStaminaTotal, msGuiTextStaminaMax, mbGuiTextStaminaActive, mbGuiTextRunningAffectStamina,
			mbGuiTextStaminaAffectRunSpeed, mbGuiTextStaminaDrain, mbGuiTextStaminaRegain
		};
		return mvStaminaSystemData;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Gets the data of the stamina system label text. This is used for debugging!
	 * 
	 * @returns cImGuiLabelData, data of the label text used to display debugging info of the stamina system.
	**/
	cImGuiLabelData GetLabelData()
	{
		cImGuiLabelData label;
		label.mFontAlign = eFontAlign_Left;
		label.mFont.mvSize = mvDebugFontSize;
		
		return label;
	}
	
    //-------------------------------------------------------
    
    //} END GUI CALLBACKS
}