#include "helpers/helper_imgui.hps"
#include "helpers/helper_player.hps"

/**
 * Creates an instance of the stamina system. This must be called outside of the cScrMap class in the [map].hps file!
 * 
 * @returns cStamina_System, an instance of the stamina system. 
**/
cStamina_System Player_StaminaSystemCreateInstance()
{
	cStamina_System staminaSystem;
	return staminaSystem;
}

class cStamina_System
{
	// Stamina system data

	// Float
	float mfStaminaTotal;
	float mfStaminaMax;
	float mfStaminaRegainMul;
	float mfStaminaUseMul;
	float mfStaminaRegainMulCounter;
	
	// Bool
	bool mbStaminaRegain;
	bool mbStaminaDrain;
	bool mbRunningAffectStamina;
	bool mbStaminaAffectRunSpeed;
	
	bool mbStaminaSystemActive;
	
	// Debug data
	cVector2f mvDebugFontSize = cVector2f(16.0f, 16.0f);
	
	cStamina_System()
	{
		// Float
		mfStaminaTotal = 100.00f;
		mfStaminaMax = 100.0f;
		mfStaminaRegainMul = 0.25f;
		mfStaminaUseMul = 1.0f;
		mfStaminaRegainMulCounter = 0.0f;
		
		// Bool
		mbStaminaRegain = true;
		mbStaminaDrain = true;
		mbRunningAffectStamina = true;
		mbStaminaAffectRunSpeed = true;
		
		mbStaminaSystemActive = false;
	}
	
	/////////////////////////////////////////
	// MAIN FUNCTIONS
	//{//////////////////////////////////////
	
	//-------------------------------------------------------
	
	/**
	 * Resets the settings to default within the current instance of the stamina system.
	**/
	void ResetStaminaSystemData()
	{
		// Float
		mfStaminaTotal = 100.00f;
		mfStaminaMax = 100.0f;
		mfStaminaRegainMul = 0.25f;
		mfStaminaUseMul = 1.0f;
		mfStaminaRegainMulCounter = 0.0f;
		
		// Bool
		mbStaminaRegain = true;
		mbStaminaDrain = true;
		mbRunningAffectStamina = true;
		mbStaminaAffectRunSpeed = true;
		
		mbStaminaSystemActive = false;
		
		return;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Runs the handlers for managing the players stamina, runs at every frame.
	 * This must be called inside the Update function within the cScrMap class in the [map].hps file!
	 * //
	 * The player has at default limited stamina to run. The stamina's value is at a range of 0 - 100.
	 * 
	 * @param float afTimeStep, the time between each step (1s/60fps = 0.01667s/fps)
	**/
	void StartStaminaSystem(float afTimeStep)
	{
		if (!GetStaminaSystemActive())
		{ 
			Error("The stamina system is not active, use Player_StaminaSystemCreateInstance().SetStaminaSystemActive(true)");
			return; 
		}
	
		StaminaRunningHandler();
		PlayerRunningSpeedHandler();
		RegainStaminaHandler();
		return;
	}
	
	//-------------------------------------------------------
	
	/**
	 * A handler for decrementing the players stamina while running, runs at every frame.
	**/
	void StaminaRunningHandler()
	{
		if (GetRunningAffectStamina())
		{
			if (mfStaminaRegainMulCounter > GetStaminaRegainMul()) { mfStaminaRegainMulCounter = GetStaminaRegainMul(); }
			//if (GetStamina() < 0) { mfStaminaTotal = 0.0f; }
			//Remove && GetStamina() >= 0.1f
			
			// Decrement the total stamina if player is running
			if (Player_GetRunning() && GetStaminaDrain() && GetStamina() >= 0.1f)
			{
				mfStaminaRegainMulCounter = 0.0f;
				mfStaminaTotal -= 0.1f*GetStaminaUsageMul();
				return;
			}
		}
	}
	
	//-------------------------------------------------------
	
	/**
	 * A handler for incrementing the players stamina while idle/walking, runs at every frame.
	 * Lowers running speed when GetStamina() < GetMaxStamina()/2.0f.
	 * Resets running speed when GetStamina() > GetMaxStamina()/2.0f.
	**/
	void PlayerRunningSpeedHandler()
	{
		if (GetStaminaAffectRunSpeed())
		{
			if (GetStamina() < GetMaxStamina()/2.0f)
			{
				Player_SetRunSpeedMul(GetStamina()/100.0f);
				return;
			}
			else if (GetStamina() > GetMaxStamina()/2.0f)
			{
				Player_SetRunSpeedMul(1.0f);
				return;
			}
			
		}
	}
	
	//-------------------------------------------------------
	
	/**
	 * A handler for incrementing the players stamina while idle/walking, runs at every frame.
	**/
	void RegainStaminaHandler()
	{
		if (!Player_GetRunning() && GetStaminaRegain() && GetStamina() < GetMaxStamina() && mfStaminaRegainMulCounter <= GetStaminaRegainMul())
		{	
			mfStaminaRegainMulCounter += 0.005f+GetStaminaRegainMul()/10.0f;
			mfStaminaTotal += 0.05f*mfStaminaRegainMulCounter;
			return;
		}
	}
	
	//-------------------------------------------------------
	
	/**
	 * Sets if stamina should drain.
	 * 
	 * @param bool abActive, if stamina should drain.
	**/
	void SetStaminaDrain(bool abActive)
	{
		mbStaminaDrain = abActive;
		if (!abActive)
		{
			SetStamina(GetMaxStamina());
			return;
		}
		return;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Gets if stamina drains.
	 * 
	 * @returns bool, checks if stamina drains.
	**/
	bool GetStaminaDrain()
	{
		return mbStaminaDrain;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Sets if stamina should regenerate.
	 * 
	 * @param bool abActive, if stamina should regenerate.
	**/
	void SetStaminaRegain(bool abActive = true)
	{
		mbStaminaRegain = abActive;
		return;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Gets if stamina regenerates.
	 * 
	 * @returns bool, if stamina regenerates.
	**/
	bool GetStaminaRegain()
	{
		return mbStaminaRegain;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Sets if running costs stamina.
	 * 
	 * @param bool abActive, if running should affect stamina.
	**/
	void SetRunningAffectStamina(bool abActive = true)
	{
		mbRunningAffectStamina = abActive;
		if (!abActive)
		{
			SetStamina(GetMaxStamina());
			return;
		}
		return;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Gets if running costs stamina.
	 * 
	 * @returns bool, checks if running affects stamina.
	**/
	bool GetRunningAffectStamina()
	{
		return mbRunningAffectStamina;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Sets if stamina affects running speed.
	 * 
	 * @param bool abActive, if stamina should affect running speed. 
	**/
	void SetStaminaAffectRunSpeed(bool abActive = true)
	{
		mbStaminaAffectRunSpeed = abActive;
		return;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Gets if stamina affects running speed.
	 * 
	 * @returns bool, checks if stamina affects running speed. 
	**/
	bool GetStaminaAffectRunSpeed()
	{
		return mbStaminaAffectRunSpeed;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Sets the stamina regain multiplier.
	 * 
	 * @param float, the value to set the stamina regain multiplier to (Max value: 10.0f, Min value: > 0). 
	**/
	void SetStaminaRegainMul(float afX = 0.25f)
	{
		if (afX > 10.0f || afX <= 0)
		{
			Error("The value range for the stamina multiplier is > 0 - 10.0, " + cString_ToString(afX, 1.0f) + " is out of range.");
			return;
		}
		else
		{
			mfStaminaRegainMul = afX;
			return;
		}
	}
	
	//-------------------------------------------------------
	
	/**
	 * Gets the stamina regain multiplier (How quickly stamina should regenerate).
	 * 
	 * @returns float, the stamina regain multiplier. 
	**/
	float GetStaminaRegainMul()
	{
		return mfStaminaRegainMul;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Sets the stamina usage multiplier (How quickly stamina should decrease).
	 * 
	 * @param float, the value to set the stamina usage multiplier to (Max value: 10.0f, Min value: > 0). 
	**/
	void SetStaminaUsageMul(float afX = 1.0f)
	{
		if (afX > 10.0f || afX <= 0)
		{
			Error("The value range for the stamina multiplier is > 0 - 10.0, " + cString_ToString(afX, 1.0f) + " is out of range.");
			return;
		}
		else
		{
			mfStaminaUseMul = afX;
		}
	}
	
	//-------------------------------------------------------
	
	/**
	 * Gets the stamina usage multiplier (How quickly stamina should decrease).
	 * 
	 * @returns float, the stamina usage multiplier. 
	**/
	float GetStaminaUsageMul()
	{
		return mfStaminaUseMul;
	}
	
	
	//-------------------------------------------------------
	
	/**
	 * Sets the player's stamina to a certain value.
	 * 
	 * @param float afX, the value to set the stamina to (Max value: GetMaxStamina(), Min value: 0.0f).
	**/
	void SetStamina(float afX = 100.0f)
	{
		if (afX > GetMaxStamina() || afX < 0)
		{
			Error("The value range for the stamina is 0.0 - " + cString_ToString(GetMaxStamina(), 1.0f) + ", " + cString_ToString(afX, 1.0f) + " is out of range.");
			return;
		}
		else
		{
			mfStaminaTotal = afX;
			return;
		}
	}
	
	//-------------------------------------------------------
	
	/**
	 * Gets the player's current stamina.
	 * 
	 * @returns float, the player's current stamina (Max value: GetMaxStamina(), Min value: 0.0f).
	**/
	float GetStamina()
	{
		return mfStaminaTotal;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Sets the maximum stamina allowed in the stamina system.
	 * 
	 * @param float afX, the maximum stamina (Max value: 1000.0f, Min value: 0.0f).
	**/
	void SetMaxStamina(float afX = 100.0f)
	{
		if (afX > 1000.0f || afX < 0)
		{
			Error("The value range for the maximum stamina is 0.0 - 1000.0, " + cString_ToString(afX, 1.0f) + " is out of range.");
			return;
		}
		else
		{
			mfStaminaMax = afX;
			mfStaminaTotal = afX;
			return;
		}
	}
	
	//-------------------------------------------------------
	
	/**
	 * Gets the maximum stamina for the stamina system.
	 * 
	 * @returns float, the maximum stamina (Max value: 1000.0f, Min value: 0.0f).
	**/
	float GetMaxStamina()
	{
		return mfStaminaMax;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Enables/Disables the stamina system. !WARNING! When disabling the stamina system, its settings reset to default.
	 * 
	 * @param bool abActive, whether the stamina system should be active or not. 
	**/
	void SetStaminaSystemActive(bool abActive = true)
	{
		if (abActive)
		{
			mbStaminaSystemActive = abActive;
			return;
		}
		else
		{
			ResetStaminaSystemData();
			return;
		}
	}
	
	//-------------------------------------------------------
	
	/**
	 * Checks if the stamina system is active.
	 * 
	 * @returns bool, checks if the stamina system is active.
	**/
	bool GetStaminaSystemActive()
	{
		return mbStaminaSystemActive;
	}

	//-------------------------------------------------------
	
	//} END MAIN FUNCTIONS
	
	/////////////////////////////////////////
	// DEBUG
	//{//////////////////////////////////////
	
	//-------------------------------------------------------
	
	/**
	 * Draws debug info of the stamina system instance, making the debugging process easier.
	 * Only draws the debug info if cLux_ScriptDebugOn() returns true.
	**/
	void DrawDebugInfo()
	{
		if (cLux_ScriptDebugOn())
		{
			array<tString> mvStaminaSystemData = GetStaminaSystemData();
			cImGuiLabelData mLabelData = GetLabelData();
			
			ImGui_DoLabelExt("Stamina: " + mvStaminaSystemData[0], mLabelData, ImGui_NrmPos(0.70f, 0.4625f, 10.0f), ImGui_NrmSize(1.0f,1.0f));
			ImGui_DoLabelExt("Max Stamina: " + mvStaminaSystemData[1], mLabelData, ImGui_NrmPos(0.61f, 0.4625f, 10.0f), ImGui_NrmSize(1.0f,1.0f));
			ImGui_DoLabelExt("StaminaSystemEnabled: " + mvStaminaSystemData[2], mLabelData, ImGui_NrmPos(0.77f, 0.4625f, 10.0f), ImGui_NrmSize(1.0f,1.0f));
			ImGui_DoLabelExt("RunAffectStamina: " + mvStaminaSystemData[3], mLabelData, ImGui_NrmPos(0.61f, 0.4835f, 10.0f), ImGui_NrmSize(1.0f,1.0f));
			ImGui_DoLabelExt("StaminaAffectRunSpeed: " + mvStaminaSystemData[4], mLabelData, ImGui_NrmPos(0.71f, 0.4835f, 10.0f), ImGui_NrmSize(1.0f,1.0f));
			ImGui_DoLabelExt("StaminaDrain: " + mvStaminaSystemData[5], mLabelData, ImGui_NrmPos(0.8325f, 0.4835f, 10.0f), ImGui_NrmSize(1.0f,1.0f));
			ImGui_DoLabelExt("StaminaRegain: " + mvStaminaSystemData[6], mLabelData, ImGui_NrmPos(0.911f, 0.4835f, 10.0f), ImGui_NrmSize(1.0f,1.0f));
			return;
		}
	}
	
	//-------------------------------------------------------
	
	/**
	 * Gets the settings of the stamina system. This is used for debugging!
	 * 
	 * @returns array<tString>, an array of the stamina system settings.
	**/
	array<tString> GetStaminaSystemData()
	{
		tString msGuiTextStaminaTotal = cString_ToString(mfStaminaTotal, 2.0f);
		tString msGuiTextStaminaMax = cString_ToString(mfStaminaMax, 2.0f);
		
		bool mbGuiTextStaminaActive = mbStaminaSystemActive;
		bool mbGuiTextRunningAffectStamina = mbRunningAffectStamina;
		bool mbGuiTextStaminaAffectRunSpeed = mbStaminaAffectRunSpeed;
		bool mbGuiTextStaminaDrain = mbStaminaDrain;
		bool mbGuiTextStaminaRegain = mbStaminaRegain;
		
		array<tString> mvStaminaSystemData = {
			msGuiTextStaminaTotal, msGuiTextStaminaMax, mbGuiTextStaminaActive, mbGuiTextRunningAffectStamina,
			mbGuiTextStaminaAffectRunSpeed, mbGuiTextStaminaDrain, mbGuiTextStaminaRegain
		};
		return mvStaminaSystemData;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Gets the data of the stamina system label text. This is used for debugging!
	 * 
	 * @returns cImGuiLabelData, data of the label text used to display debugging info of the stamina system.
	**/
	cImGuiLabelData GetLabelData()
	{
		cImGuiLabelData label;
		label.mFontAlign = eFontAlign_Left;
		label.mFont.mvSize = mvDebugFontSize;
		
		return label;
	}
	
	//-------------------------------------------------------
	
	//} END DEBUG
}