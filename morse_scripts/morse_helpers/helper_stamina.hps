#include "helpers/helper_effects.hps"
#include "helpers/helper_audio.hps"
#include "helpers/helper_imgui.hps"
#include "helpers/helper_player.hps"


/**
 * Creates an instance of the stamina system. This must be called outside of the cScrMap class in the [map].hps file!
 * 
 * @returns cStamina_System, an instance of the stamina system. 
**/
cStamina_System Player_StaminaSystemCreateInstance()
{
	cStamina_System staminaSystem;
	return staminaSystem;
}

class cStamina_System
{
	// Stamina system data
	float mfStaminaTotal;
	float mfStaminaMax;
	bool mbStaminaSystemActive;

	
	// Debug data
	cVector2f mvDebugFontSize = cVector2f(16.5f, 16.5f);
	
	cStamina_System()
	{
		mfStaminaTotal = 100.00f;
		mfStaminaMax = 100.0f;
		mbStaminaSystemActive = false;
	}
	
	/////////////////////////////////////////
	// MAIN FUNCTIONS
	//{//////////////////////////////////////
	
	//-------------------------------------------------------
	
	/* TODO:
	 * Add a method that disables/enables stamina regain
	 * Add a method that disables/enables jump stamina cost
	 * Add a method that disables/enables running stamina cost
	 * Add a method that disables/enables crouch slow stand up motion
	 * 
	 * Add a method that disables/enables staminas effect on ladder climbing speed
	 * Add a method that disables/enables staminas effect on running speed
	 * Add a method that disables/enables staminas effect on the ability to jump
	 */
	
	/* 
	 * Player_GetCanRun()
	 * Player_SetCanRun()
	 * Player_GetRunning()
	 * Player_GetRunSpeedMul()
	 * Player_SetMoveSpeedMul() / Player_FadeMoveSpeedMulTo()
	 * Player_SetAllMoveSpeedMuls() / Player_FadeAllMoveSpeedMulsTo()
	 * Player_GetRunSpeedMul()
	 * Player_SetRunSpeedMul() / Player_FadeRunSpeedMulTo()
	 * Player_SetJumpDisabled() - if stamina < 15.0
	 * Player_SetSlowStandupMotion()
	 * Player_SetLadderMoveMul()
	 * Player_SetCrouchSpeedMul()
	*/
	
	/**
	 * A handler for managing the players stamina, runs at every frame.
	 * This must be called inside the Update function within the cScrMap class in the [map].hps file!
	 * //
	 * The player has limited stamina to run, jump (cannot jump at stamina > 15.0),
	 * crouch (slows down crouching animation at stamina < 50.0). The stamina's value is at a range of 0 - 100.
	**/
	void StaminaHandler(float afTimeStep)
	{
		if (!this.mbStaminaSystemActive) { return; }
		
		// Decrement the total stamina if player is running
		if (Player_GetRunning() && this.mfStaminaTotal >= 0.1f)
		{
			this.mfStaminaTotal -= 0.1f;
		}
		else if (!Player_GetRunning() && this.mfStaminaTotal < this.mfStaminaMax) // Increment the total stamina if player is not running
		{
			this.mfStaminaTotal += 0.05f;
		}
	}
	
	//-------------------------------------------------------
	
	/**
	 * Resets the data within the current instance of the stamina system.
	**/
	void ResetStaminaSystemData()
	{
		this.mfStaminaTotal = 100.00f;
		this.mfStaminaMax = 100.0f;
		this.mbStaminaSystemActive = false;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Sets the player's stamina to a certain value.
	 * 
	 * @param float afX, the value to set the stamina to (Max value: GetMaxStamina(), Min value: 0.0f).
	**/
	void SetStamina(float afX)
	{
		if (afX > GetMaxStamina() || afX < 0)
		{
			Error("The value range for the stamina is 0.0 - " + cString_ToString(GetMaxStamina(), 1.0f) + ", " + cString_ToString(afX, 1.0f) + " is out of range.");
			return;
		}
		else
		{
			this.mfStaminaTotal = afX;
			return;
		}
	}
	
	//-------------------------------------------------------
	
	/**
	 * Gets the player's current stamina.
	 * 
	 * @returns float, the player's current stamina (Max value: GetMaxStamina(), Min value: 0.0f).
	**/
	float GetStamina()
	{
		return this.mfStaminaTotal;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Sets the maximum stamina allowed in the stamina system.
	 * 
	 * @param float afX, the maximum stamina (Max value: 1000.0f, Min value: 0.0f).
	**/
	void SetMaxStamina(float afX = 100.0f)
	{
		if (afX > 1000.0f || afX < 0)
		{
			Error("The value range for the maximum stamina is 0.0 - 1000.0, " + cString_ToString(afX, 1.0f) + " is out of range.");
			return;
		}
		else
		{
			this.mfStaminaMax = afX;
			this.mfStaminaTotal = afX;
			return;
		}
	}
	
	//-------------------------------------------------------
	
	/**
	 * Gets the maximum stamina for the stamina system.
	 * 
	 * @returns float, the maximum stamina (Max value: 1000.0f, Min value: 0.0f).
	**/
	float GetMaxStamina()
	{
		return this.mfStaminaMax;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Enables/Disables the stamina system.
	 * 
	 * @param bool abActive, whether the stamina system should be active or not. 
	**/
	void SetStaminaSystemActive(bool abActive = true)
	{
		if (abActive)
		{
			this.mbStaminaSystemActive = abActive;
			return;
		}
		else
		{
			this.ResetStaminaSystemData();
			return;
		}
	}
	
	//-------------------------------------------------------
	
	/**
	 * Checks if the stamina system is active.
	 * 
	 * @returns bool, checks whether the stamina system is active or not.
	**/
	bool GetStaminaSystemActive()
	{
		return this.mbStaminaSystemActive;
	}
	
	//-------------------------------------------------------
	
	//} END MAIN FUNCTIONS
	
	/////////////////////////////////////////
	// DEBUG
	//{//////////////////////////////////////
	
	//-------------------------------------------------------
	
	/**
	 * Draws debug info of the stamina system instance, making the debugging process easier.
	 * Only draws the debug info if cLux_ScriptDebugOn() returns true.
	**/
	void DrawDebugInfo()
	{
		if (cLux_ScriptDebugOn())
		{
			array<tString> mvStaminaSystemData = GetStaminaSystemData();
			ImGui_DoLabelExt("Stamina: " + mvStaminaSystemData[0], GetLabelData(), ImGui_NrmPos(0.14f, 0.4835f, 10.0f), ImGui_NrmSize(1.0f,1.0f));
			ImGui_DoLabelExt("StaminaEnabled: " + mvStaminaSystemData[1], GetLabelData(), ImGui_NrmPos(0.22f, 0.4835f, 10.0f), ImGui_NrmSize(1.0f,1.0f));
			return;
		}
	}
	
	//-------------------------------------------------------
	
	/**
	 * Gets the data of the stamina system. This is used for debugging!
	 * 
	 * @returns array<tString>, an array of the stamina system data
	**/
	array<tString> GetStaminaSystemData()
	{
		tString msGuiTextStaminaTotal = cString_ToString(this.mfStaminaTotal, 2.0f);
		tString msGuiTextStaminaMax = cString_ToString(this.mfStaminaTotal, 2.0f);
		bool mbGuiTextStaminaActive = this.mbStaminaSystemActive;
		array<tString> mvStaminaSystemData = {msGuiTextStaminaTotal, mbGuiTextStaminaActive};
		return mvStaminaSystemData;
	}
	
	//-------------------------------------------------------
	
	/**
	 * Gets the data of the stamina system. This is used for debugging!
	 * 
	 * @returns array<tString>, an array of the stamina system data
	**/
	cImGuiLabelData GetLabelData()
	{
		cImGuiLabelData label;
		label.mFontAlign = eFontAlign_Center;
		label.mFont.mvSize = this.mvDebugFontSize;
		
		return label;
	}
	
	//-------------------------------------------------------
	
	//} END DEBUG
}